grammar 'Grammar';

Grammar = 'grammar' StrLit ';' RuleList ;

RuleList = Rule* ;

Rule = Name '=' ClauseList ';' 
     | Name ':' Name '=' ClauseList ';'
     | Name '.' Name ':' Name '=' ClauseList ';'
     | '.' Name ':' Name '=' ClauseList ';' ;

ClauseList = ClauseItemList + ~'|' ;

ClauseItemList = ClauseExpr * ;

ClauseExpr = ',' ClauseExpr1
                | '!' ClauseExpr1
                | ,ClauseExpr1 ;

ClauseExpr: ClauseExpr1 = ClauseExpr2 '*' OptDelim
                | ClauseExpr2 '+' OptDelim
                | ClauseExpr2 '?'
                | ,ClauseExpr2 ;

ClauseExpr: ClauseExpr2 = '(' ClauseList ')'
                | Name
                | StrLit
                | '.'
                | regexplit ;

OptDelim = ('~' ClauseExpr2)? ;

StrLit = str ;
Name = id ;

id = [a-zA-Z][A-Za-z0-9_]* ;
str = '\'' ([^'] | '\\\'')* '\'' ;
regexplit = '[' ([^\]] | '\\]')* ']' ;
Ignore: ws = [ \t\n]+ ;
Ignore: comment = '#' .* '\n' ;
Ignore: comment1 = '/*' ([^\r]|[^\n]|.)* '*/';

