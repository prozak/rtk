grammar 'Grammar';

Grammar = 'grammar' StrLit ';' RuleList ;

RuleList = Rule *;

Rule = Name '=' Clause ';' 
     | Name ':' Name '=' Clause ';'
     | Name '.' Name ':' Name '=' Clause ';'
     | '.' Name ':' Name '=' Clause ';' ;

Clause = Clause '|' Clause2
         | ,Clause2;

Clause: Clause2 = Clause2 Clause3
                  | ,Clause3 ;

Clause: Clause3 = ',' Clause4 
                | '!' Clause4
                | ,Clause4 ;

Clause: Clause4 = Clause5 '*' OptDelim
                | Clause5 '+' OptDelim
                | Clause5 '?'
                | ,Clause5 ;

Clause: Clause5 = '(' ,Clause ')'
                | Name
                | StrLit
                | '.'
                | regexplit ;

OptDelim = ('~' Clause5)? ;

StrLit = str ;
Name = id ;

id = [a-zA-Z][A-Za-z0-9_]* ;
str = '\'' ([^'] | '\\\'')* '\'' ;
regexplit = '[' ([^\]] | '\\]')* ']' ;
Ignore: ws = [ \t\n]+ ;
Ignore: comment = '#' .* '\n' ;

